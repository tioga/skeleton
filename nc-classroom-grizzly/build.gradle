project(":nc-classroom-grizzly") {

    apply plugin: 'distribution'

    version = tiogaAppVersion

    dependencies {
        compile(project(":nc-classroom-engine"))
        compile("org.glassfish.jersey.ext:jersey-spring3:2.17")
        compile("org.springframework:spring-aop:4.1.6.RELEASE") { force = true }
        compile("org.springframework:spring-core:4.1.6.RELEASE") { force = true }
        compile("org.springframework:spring-beans:4.1.6.RELEASE") { force = true }
        compile("org.springframework:spring-context:4.1.6.RELEASE") { force = true }

        compile("org.tiogasolutions.lib:tioga-lib-spring:+")
        compile("org.tiogasolutions.runners:tioga-runners-grizzly:+")

        // required by Jersey-Spring's stupid attempt to check for a ServletContext
        compile("javax.servlet:javax.servlet-api:3.1.0")
    }

    configurations.all({
        resolutionStrategy({
            force(
                    "commons-codec:commons-codec:1.6",

                    "org.springframework:spring-web:4.1.6.RELEASE",
                    "org.springframework:spring-aop:4.1.6.RELEASE",
                    "org.springframework:spring-core:4.1.6.RELEASE",
                    "org.springframework:spring-beans:4.1.6.RELEASE",
                    "org.springframework:spring-context:4.1.6.RELEASE",
                    "org.springframework:spring-webmvc:4.1.6.RELEASE",
                    "org.springframework:spring-expression:4.1.6.RELEASE",
            )
        })
    })

    distributions {
        main {
            contents {
                into("lib") {
                    from(jar)
                    from(project.configurations.runtime)
                }
            }
        }
    }

    jar {
        into("libs") {
            from configurations.runtime
        }
        manifest {
            attributes(
                    "Implementation-Version": project.version,
                    "Main-Class": "org.tiogasolutions.classroom.server.grizzly.ClassroomMainWrapper",
                    "Class-Path": configurations.compile.collect { "./libs/" + it.getName() }.join(" ")
            )
        }
    }

    apply(from: urlCache.get("https://raw.githubusercontent.com/tioga/build-scripts/master/build-config-deployer.gradle"))

    task pws(type: Copy, dependsOn: assemble) {
        from "./build/libs/nc-classroom-grizzly-${project.version}.jar"
        into "./build/pws/"
        rename "nc-classroom-grizzly-${project.version}.jar", "nc-classroom-grizzly.jar"
    }
}











